buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        alpnBootVersion   = '8.1.7.v20160121'
		jettyVersion      = '9.3.8.v20160314'
    }

    repositories {
        jcenter()
        maven { url "https://repo.spring.io/release" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
  id 'java'
  id 'application'
  id 'eclipse'
  id 'idea'
}
apply plugin: 'spring-boot'


group = 'org.test'
version = '0.0.1-SNAPSHOT'

description = "demo-http2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    jvmBoot { description "additional libs on jvm bootclasspath" }
}

dependencyManagement {
	imports {
		ext['jetty.version'] = "${jettyVersion}"
	  mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
	}
  }

repositories {
     jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework:spring-webmvc')
	
	// jetty
    compile('org.springframework.boot:spring-boot-starter-jetty')
	compile("org.eclipse.jetty.http2:http2-server:${jettyVersion}")
	compile("org.eclipse.jetty:jetty-alpn-server:${jettyVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }

	// ALPN
    compile "org.mortbay.jetty.alpn:alpn-boot:${alpnBootVersion}"
    jvmBoot "org.mortbay.jetty.alpn:alpn-boot:${alpnBootVersion}"
}

defaultTasks 'bootRun'
mainClassName = 'demo.DemoHttp2Application'
applicationDefaultJvmArgs = ["-Xbootclasspath/p:lib/alpn-boot-${alpnBootVersion}.jar"]

distributions {
    main.contents {
        from(configurations.jvmBoot) {
            into "lib"
        }
    }
}

task copyAlpnBootJar (type:Copy) {
    from (configurations.jvmBoot)
    into("lib")
}

tasks["eclipse"].dependsOn(copyAlpnBootJar)
processResources.dependsOn copyAlpnBootJar

clean {
    delete "lib/*.jar"
}
