apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

group = 'org.test'
version = '0.0.1-SNAPSHOT'

description = "demo-http2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
	jvmBoot { description "additional libs on jvm bootclasspath" }
}

repositories {
     jcenter()
     maven { url "https://repo.spring.io/snapshot" }
     maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:'1.3.0.RC1') {
		exclude(module: 'spring-boot-starter-tomcat')
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-undertow', version:'1.3.0.RC1'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'1.3.0.RC1') {
		exclude(module: 'commons-logging')
    }
	// make sure we use the latest alpn-boot
	jvmBoot 'org.mortbay.jetty.alpn:alpn-boot:8.1.+'
}

defaultTasks 'run'
mainClassName = 'demo.DemoHttp2Application'
applicationDefaultJvmArgs = ["-Xbootclasspath/p:$configurations.jvmBoot.singleFile"]

distributions {
	main.contents {
		from(configurations.jvmBoot) {
			into "lib"
		}
	}
}

startScripts {
	doLast {
		// replace absolute path to relative one.
		unixScript.text = unixScript.text.replaceAll('-Xbootclasspath/p:.*alpn-boot-8.1.', '-Xbootclasspath/p:lib/alpn-boot-8.1.')
		windowsScript.text = windowsScript.text.replaceAll('-Xbootclasspath/p:.*alpn-boot-8.1.', '-Xbootclasspath/p:lib/alpn-boot-8.1.')
	}
}
